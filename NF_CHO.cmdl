// CHO Model (adapted from previous NF model)

b = 15; // binding rate of inducer to TetR::EGFP
lk = 2; // TetR::EGFP basal transcriptional rate (leakage)
m_eff = 200; // TetR::EGFP max transcriptional rate
kappa = 100; // Divider for leak
mu = 3.5; // TetR::EGFP mRNA degradation
p = 75/2; // TetR::EGFP translational rate (/2 due to TetR dimers)
delta = 0.046; // TetR::EGFP degradation + dilution
a = 3.7; // rate of transcription machinery assembling
alpha = 0.28; // rate of transcription machinery disassembling
rho = 20.8; // rate of TetR::EGFP unbinding from the promoter
r = 0.2; // rate of TetR::EGFP binding to the promoter
//r = 0; // tetR mutant, no binding to promoter
f = 2.5*0.9242; // rate of inducer entering the cells [0..infinity], multiplied by 2.5 to convert to CHO data
c = 140; // effective inducer concentration constant modifier
 // (increase it for cells which are easily penetrated)

M = 0; // TetR::EGFP mRNA
D = 0; // free TetR (total TetR will be D+H+B)
G = 0; // free GFP (separated from TetR due to P2A)
H = 0; // 1 inducer-bound TetR
B = 0; // 2 inducer-bound TetR
TOTAL_PROTEIN = [D+H+B]; // total tetR protein, deferred expression

 // Promoter states:
R01 = 0; // one tetO2 site occupied (single repressed state)
R02 = 1; // two tetO2 sites occupied (double repressed state)
A = 0; // unrepressed state
Aup = 1; // promoter state with transcription machinery present
Aup0 = 0; // promoter state w/o transcription machinery (inactive)
Ii = 0; // intracellular concentration of inducer
Ie = 0.05; // extracellular concentration of inducer, here small amount of eDox

Iinflux, Ie->Ie+Ii, f*c;
Ioutflux, Ii->, f;
r3, Aup->Aup0, alpha;
r4, Aup0->Aup, a;
r5, Aup+A->Aup+A+M, m_eff;
r5_2, R01 -> R01+M, m_eff/kappa;
r5_3, R02 -> R02+M, m_eff/kappa;
r6, M->, mu;
r7, M->M+D+G, p; 
r8, D->, delta;
r9, D+Ii->H, 2*b;
r10, H->, delta;
r11, H+Ii->B, b;
r12, B->, delta;
r13, G->, delta;
s01, D+A->R01, r;
s02, R01->D+A, rho; 
s03, D+R01->R02, r;
s04, R02->D+R01, rho; 

